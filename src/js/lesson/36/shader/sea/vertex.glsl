/**
 * @description Sea vertex shader
 * @author      C. M. de Picciotto <d3p1@d3p1.dev> (https://d3p1.dev/)
 */
uniform float uTime;
uniform float uNormalShift;
uniform float uBigWavesElevation;
uniform vec2  uBigWavesFrequency;
uniform float uBigWavesSpeed;
uniform float uSmallWavesElevation;
uniform float uSmallWavesFrequency;
uniform float uSmallWavesSpeed;
uniform float uSmallWavesIterations;

varying vec3 vPosition;
varying vec3 vNormal;

#include ../utils/calcPerlinNoise.glsl
#include ./utils/calcWave.glsl

void main() {
    vec4 modelPosition  = modelMatrix * vec4(position, 1.0);
    vec3 iUnitPosition  = modelPosition.xyz + vec3(uNormalShift, 0.0, 0.0);
    vec3 jUnitPosition  = modelPosition.xyz + vec3(0.0, 0.0, -uNormalShift);
    modelPosition.xyz   = calcWave(
        modelPosition.xyz,
        uBigWavesFrequency,
        uBigWavesElevation,
        uBigWavesSpeed,
        uSmallWavesFrequency,
        uSmallWavesElevation,
        uSmallWavesSpeed,
        uSmallWavesIterations,
        uTime
    );
    iUnitPosition = calcWave(
        iUnitPosition.xyz,
        uBigWavesFrequency,
        uBigWavesElevation,
        uBigWavesSpeed,
        uSmallWavesFrequency,
        uSmallWavesElevation,
        uSmallWavesSpeed,
        uSmallWavesIterations,
        uTime
    );
    jUnitPosition = calcWave(
        jUnitPosition.xyz,
        uBigWavesFrequency,
        uBigWavesElevation,
        uBigWavesSpeed,
        uSmallWavesFrequency,
        uSmallWavesElevation,
        uSmallWavesSpeed,
        uSmallWavesIterations,
        uTime
    );
    vec3 iUnitVector = normalize(iUnitPosition - modelPosition.xyz);
    vec3 jUnitVector = normalize(jUnitPosition - modelPosition.xyz);

    vec4 viewPosition       = viewMatrix * modelPosition;
    vec4 projectionPosition = projectionMatrix * viewPosition;

    gl_Position = projectionPosition;
    vPosition   = modelPosition.xyz;
    vNormal     = cross(iUnitVector, jUnitVector);
}